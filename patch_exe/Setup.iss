; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyPatchName "Example Application 1.01"
#define MyAppName "Example Application"
#define MyAppVersion "1.01"
#define MyAppPublisher "Example Corporation"
#define MyAppURL "http:/example.com/"
#define MyAppExeName "example.exe"

#define MyPatchSourceDir "..\src_app_v2"
#define MySetupResourceDir "..\setup_resources"

#define MyAppId "{{GUID-GUID-GUID-GUID-GUID}"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={#MyAppId}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppVerName={#MyAppName}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}

DefaultDirName={pf}\{#MyAppPublisher}\{#MyAppName}
;           or {pf}\{#MyAppPublisher} {#MyAppName}
;           or {pf}\{#MyAppName}

DefaultGroupName={#MyAppPublisher} {#MyAppName}
;             or {#MyAppName}

OutputDir=DISK1
OutputBaseFilename=setup
SolidCompression=yes

; InfoAfterFile=README.txt

UninstallDisplayIcon={app}\{#MyAppExeName},0
SetupIconFile={#MySetupResourceDir}/Setup.ico
WizardImageFile={#MySetupResourceDir}/SetupWizImage.bmp
WizardSmallImageFile={#MySetupResourceDir}/SetupWizSmallImage.bmp

; for Patch
UsePreviousAppDir=yes
CreateUninstallRegKey=no
UpdateUninstallLogAppName=no
; DisableDirPage=yes


[Files]
Source: "{#MyPatchSourceDir}\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\example_v2.exe"


[Languages]
;Name: "english"; MessagesFile: "compiler:Default.isl"
Name: "japanese"; MessagesFile: "compiler:Languages\Japanese.isl"

[CustomMessages]
;english.PatchTargetIsNotInstalled=Application '{#MyAppName}' is not installed. This setup requires the target application installed. Do you want to continue?
japanese.PatchTargetIsNotInstalled=アプリケーション '{#MyAppName}' がインストールされていません。このセットアップは修正差分で対象となるアプリケーションが必要です。続行しますか？

[Code]

// Block setup if the application is not installed.
function InitializeSetup(): Boolean;
begin
  if Not RegKeyExists(HKLM, ExpandConstant('SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\{#MyAppId}_is1')) then
    Result := MsgBox(ExpandConstant('{cm:PatchTargetIsNotInstalled}'), mbConfirmation, MB_YESNO) = IDYES
  else
    Result := True;
end;
